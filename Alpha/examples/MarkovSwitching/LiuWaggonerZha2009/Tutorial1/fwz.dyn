//Declarations: names are separated by a comma, a space or both
var	 X, PAI, R,  ZS, ZD;

varexo ES, ED, ER;

parameters tau, beta, kappa, rhor, gamma_1, gamma_2
rhod rhos sigd sigs	sigr
// constant transition probabilities from a chain we call "nk"
//N.B: The chain name could be anything as long as it does not contain "_" (underscores)
// a valid transition probability name contains exactly 3 underscores
// N.B: In RISE, we declare only the off-diagonal elements of the transition matrix
nk_tp_1_2, nk_tp_2_1;


model(linear)
	// N.B: time is denoted by () as in dynare or by {}. Below, we use the {} notation

   // Main equations
   X   = X{+1}-tau*(R-PAI{+1})+ZD;
   
   PAI = beta*PAI{+1}+kappa*X+ZS;
   
   R   = rhor*R{-1}+(1-rhor)*(gamma_1*PAI+gamma_2*X)+sigr*ER;

   // Shock processes
   ZD = rhod*ZD{-1}+sigd*ED;
   
   ZS = rhos*ZS{-1}+sigs*ES;
end

parameterization
	@#if indx_model==0
		@# include "param_original.dyn"
	@#elseif indx_model==1
		@# include "param_policyChangeOnly.dyn"
	@#elseif indx_model==2
		@# include "param_volatilityChangeOnly.dyn"
	@#elseif indx_model==3
		@# include "param_privateChangeOnly.dyn"
	@#end
	// constant transition probabilities
	// those parameters do not change across the
	// specifications above and so they are included in this file
	nk_tp_1_2,   0.0128;
	nk_tp_2_1,   0;	
end
