var	DPQ_P_NW $Inflation$, D_GDP_NW $Growth$, I, PAI, R, RN3M_NW $Fed Funds Rate$, Y, ZGDP, ZI, ZPAI, ZY;

varexo EGDP $output shock$,EI $monetary policy shock$,EPAI $Cost push shock$,EY $IS shock$;

parameters beta_lag $\beta_{lag}$, beta_lead $\beta_{lead}$, beta_r $\beta_{r}$,
gam_lag $\gamma_{lag}$, gam_y $\gamma_{y}$, gyss, iss, lamb_lag $\lambda_{lag}$,
lamb_lead $\lambda_{lead}$, lamb_y $\lambda_{y}$, paiss,
rhogdp $\rho_{gdp}$, rhoi $\rho_{i}$, rhopai $\rho_{\pi}$, rhoy $\rho_{y}$,
siggdp $\sigma_{gdp}$, sigi $\sigma_{i}$, sigpai $\sigma_{\pi}$, sigy $\sigma_{y}$;

parameters a_tp_1_2 a_tp_2_1 ;
parameters b_tp_1_2 b_tp_2_1 ;
				                              						 
model(linear);
   Y=beta_lag*Y(-1)+beta_lead*Y(+1)-beta_r*R(-1)+ZY;

   PAI=lamb_lag*PAI(-1)+lamb_lead*PAI(+1)+lamb_y*Y(-1)+ZPAI;

   I=gam_lag*I(-1)+(1-gam_lag)*(PAI(+4)+gam_y*Y)+ZI;

   R=I-PAI(+1);

   D_GDP_NW=Y-Y(-1)+ZGDP;

   DPQ_P_NW=paiss+PAI;

   RN3M_NW=iss+I;

   ZI=rhoi*ZI(-1)+sigi*EI;
   
   ZPAI=rhopai*ZPAI(-1)+sigpai*EPAI;
   
   ZY=rhoy*ZY(-1)+sigy*EY;
   
   ZGDP=(1-rhogdp)*gyss+rhogdp*ZGDP(-1)+siggdp*EGDP;
end;

varobs DPQ_P_NW, D_GDP_NW, RN3M_NW;


parameterization;
// not estimated
	gyss   		     ,0 	      ;						 
	iss    		     ,0 	      ;						 
	paiss  		     ,0 	      ;
// Controlled by the first chain
	beta_lag(1,a)    ,0.5000      ,0	   ,.99;						 
	beta_lead(1,a)   ,0.0003	  ,0	   ,.99;						 
	beta_r(1,a)  	 ,0.6000	  ,0	   ,3.0;						 
	gam_lag(1,a) 	 ,0.7000	  ,0	   ,.99;						 
	gam_y(1,a)   	 ,0.5000	  ,0	   ,.99;						 
	lamb_lag(1,a)	 ,0.8000	  ,0	   ,.99;						 
	lamb_lead(1,a)   ,0.0001	  ,0	   ,.99;						 
	lamb_y(1,a)  	 ,0.3000	  ,0	   ,2.0;
	
	beta_lag(2,a)    ,0.4000      ,0	   ,.99;						 
	beta_lead(2,a)   ,0.0001	  ,0	   ,.99;						 
	beta_r(2,a)  	 ,0.3000	  ,0	   ,3.0;						 
	gam_lag(2,a) 	 ,0.1000	  ,0	   ,.99;						 
	gam_y(2,a)   	 ,0.2500	  ,0	   ,.99;						 
	lamb_lag(2,a)	 ,0.3000	  ,0	   ,.99;						 
	lamb_lead(2,a)   ,0.0001	  ,0	   ,.99;						 
	lamb_y(2,a)  	 ,0.3000	  ,0	   ,2.0;
// Controlled by the second chain	
	rhogdp(b,1)	     ,0.5000	  ,0	   ,.99;						 
	rhoi(b,1)	     ,0.5000	  ,0	   ,.99;						 
	rhopai(b,1)	     ,0.5000	  ,0	   ,.99;						 
	rhoy(b,1)	     ,0.5000      ,0	   ,.99;						 
	siggdp(b,1)	     ,0.0010	  ,0.0001  ,3.0;						 
	sigi(b,1)	     ,0.0010	  ,0.0001  ,3.0;						 
	sigpai(b,1)	     ,0.0010	  ,0.0001  ,3.0;						 
	sigy(b,1)	     ,0.0010	  ,0.0001  ,3.0;
	
	rhogdp(b,2)	     ,0.0050	  ,0	   ,.99;						 
	rhoi(b,2)	     ,0.0050	  ,0	   ,.99;						 
	rhopai(b,2)	     ,0.0050	  ,0	   ,.99;						 
	rhoy(b,2)	     ,0.0050      ,0	   ,.99;						 
	siggdp(b,2)	     ,0.0200	  ,0.0001  ,3.0;						 
	sigi(b,2)	     ,0.0200	  ,0.0001  ,3.0;						 
	sigpai(b,2)	     ,0.0200	  ,0.0001  ,3.0;						 
	sigy(b,2)	     ,0.0200	  ,0.0001  ,3.0;

// transition probabilities
   a_tp_1_2  	    ,0.0100   	 ,0.0001  ,.9;
   a_tp_2_1  	    ,0.0100   	 ,0.0001  ,.6 ; 
   b_tp_1_2  	    ,0.0100   	 ,0.0001  ,.9;
   b_tp_2_1  	    ,0.0100   	 ,0.0001  ,.6 ; 
end;		  	                              
