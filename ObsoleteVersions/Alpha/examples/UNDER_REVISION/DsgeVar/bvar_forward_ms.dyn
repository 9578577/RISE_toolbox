var a g mc mrs n winf pie r rw y;

varexo e_a e_g e_lam e_ms;

parameters invsig delta gam rho gampie gamy rhoa rhog bet 
	   thetabig omega eps, sig_a, sig_g, sig_ms, sig_lam
	   dsge_prior_weight;

model(linear);

	y=y(+1)-(1/invsig)*(r-pie(+1)+g(+1)-g);
	y=a+(1-delta)*n;
	mc=rw+n-y;
	mrs=invsig*y+gam*n-g;
	r=rho*r(-1)+(1-rho)*(gampie*pie+gamy*y)+sig_ms*e_ms;
	rw=rw(-1)+winf-pie;
	a=rhoa*a(-1)+sig_a*e_a;
	g=rhog*g(-1)+sig_g*e_g;
	rw=mrs;

//	//$ HYBRID PHILLIPS CURVED USED FOR THE SUMULATIONS:
	   pie = (omega/(1+omega*bet))*pie(-1)+(bet/(1+omega*bet))*pie(1)+(1-delta)*
      (1-(1-1/thetabig)*bet)*(1-(1-1/thetabig))/((1-1/thetabig)*(1+delta*(eps-1)))/(1+omega*bet)*(mc+sig_lam*e_lam);

//	//$ FORWARD LOOKING PHILLIPS CURVE:
//	    pie=bet*pie(+1)+(1-delta)*(1-(1-1/thetabig)*bet)*(1-(1-1/thetabig))/((1-1/thetabig)*(1+delta*(eps-1)))*(mc+sig_lam*e_lam);
end;


varobs pie r rw y;

parameterization;
	omega   ,0.54;
	eps     ,6;
	bet     ,0.99;
	delta   ,0.36;
	
    sig_a   , 0.6252,0,2,uniform_pdf,1 ;
    sig_g   , 0.5951,0,2 ;
    sig_ms  , 0.7619,0,2 ;
    sig_lam , 1.2599,0,2 ;

	dsge_prior_weight,.8, 0, 20;
    invsig  , 1.8052, 0.5,5,gamma_pdf, 0.9;
    gam     , 1.0343, 0, 2, normal_pdf, 0.9;
    rho     , 0.2173 ,0.2,.8,beta_pdf,0.9;
    gampie  , 1.6377, 1.2,2,normal_pdf, 0.9;
    gamy    , 0.0704, 0.03,0.2,gamma_pdf, 0.9;
    rhoa    , 0.4961 ,0.2,.8,beta_pdf,0.9;
    rhog    , 0.6564 ,0.2,.8,beta_pdf,0.9;
    thetabig, 2.1796, 1, 3, gamma_pdf, 0.9;
end;


////RESULTS FROM POSTERIOR MAXIMIZATION
////parameters
////     prior mean     mode    s.d. t-stat prior pstdev
////
////invsig   2.500   1.8052  0.3991  4.5229 gamm  1.7600
////gam      1.000   1.0343  0.3555  2.9093 norm  0.5000
////rho      0.500   0.2173  0.1616  1.3443 unif  0.2887
////gampie   1.500   1.6377  0.2257  7.2568 norm  0.2500
////gamy     0.125   0.0704  0.0527  1.3358 gamm  0.0750
////rhoa     0.500   0.4961  0.0793  6.2582 unif  0.2887
////rhog     0.500   0.6564  0.0858  7.6519 unif  0.2887
////thetabig   3.000   2.1796  0.2001 10.8916 gamm  1.4200
////standard deviation of shocks
////     prior mean     mode    s.d. t-stat prior pstdev
////
////e_a      1.000   0.6252  0.1735  3.6024 unif  0.5774
////e_g      1.000   0.5951  0.1191  4.9975 unif  0.5774
////e_ms     1.000   0.7619  0.0995  7.6594 unif  0.5774
////e_lam    1.000   1.2599  0.3387  3.7197 unif  0.5774
////
////estimation(datafile=datarabanal_hybrid,first_obs=50,mh_nblocks = 1,nobs=90,mode_compute=4,mh_replic=20000,bayesian_irf);